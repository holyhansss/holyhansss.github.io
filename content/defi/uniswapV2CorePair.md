---
emoji: ğŸ§¢
title: Uniswap V2 Core Pair Contract ë¶„ì„
date: '2022-04-27 21:42:00'
author: í•œì„±ì›
tags: blog Defi Uniswap decentralizedExchange Dao Blockchain
categories: Defi
---


# ğŸ‘‹ Uniswap V2 Core Pair
ì €ë²ˆì£¼ì— [Uniswap Factory Contact](https://holyhansss.github.io/defi/uniswapV2CoreFactory/) ë¶„ì„ì„ í•´ë´¤ë‹¤. FactoryëŠ” ìƒê°ë³´ë‹¤ëŠ” ì½”ë“œê°€ ì‰¬ì› ì§€ë§Œ Pairì€ ì¡°ê¸ˆ ë” ì–´ë ¤ì›Œ ë³´ì¸ë‹¤! 

Pair ContactëŠ” [ì—¬ê¸°ì„œ](https://github.com/Uniswap/v2-core/blob/master/contracts/UniswapV2Pair.sol) í™•ì¸í•  ìˆ˜ ìˆê³  Pair ë¶„ì„ ì „ [Factory Contract ë¶„ì„](https://holyhansss.github.io/defi/uniswapV2CoreFactory/)ì„ ë³´ê³ ì˜¤ë©´ ì¢‹ì„ ê²ƒ ê°™ë‹¤.

## Uniswap V2 Pair Interface

```solidity
interface IUniswapV2Pair {
    event Approval(address indexed owner, address indexed spender, uint value);
    event Transfer(address indexed from, address indexed to, uint value);

    function name() external pure returns (string memory);
    function symbol() external pure returns (string memory);
    function decimals() external pure returns (uint8);
    function totalSupply() external view returns (uint);
    function balanceOf(address owner) external view returns (uint);
    function allowance(address owner, address spender) external view returns (uint);

    function approve(address spender, uint value) external returns (bool);
    function transfer(address to, uint value) external returns (bool);
    function transferFrom(address from, address to, uint value) external returns (bool);

    function DOMAIN_SEPARATOR() external view returns (bytes32);
    function PERMIT_TYPEHASH() external pure returns (bytes32);
    function nonces(address owner) external view returns (uint);

    function permit(address owner, address spender, uint value, uint deadline, uint8 v, bytes32 r, bytes32 s) external;

    event Mint(address indexed sender, uint amount0, uint amount1);
    event Burn(address indexed sender, uint amount0, uint amount1, address indexed to);
    event Swap(
        address indexed sender,
        uint amount0In,
        uint amount1In,
        uint amount0Out,
        uint amount1Out,
        address indexed to
    );
    event Sync(uint112 reserve0, uint112 reserve1);

    function MINIMUM_LIQUIDITY() external pure returns (uint);
    function factory() external view returns (address);
    function token0() external view returns (address);
    function token1() external view returns (address);
    function getReserves() external view returns (uint112 reserve0, uint112 reserve1, uint32 blockTimestampLast);
    function price0CumulativeLast() external view returns (uint);
    function price1CumulativeLast() external view returns (uint);
    function kLast() external view returns (uint);

    function mint(address to) external returns (uint liquidity);
    function burn(address to) external returns (uint amount0, uint amount1);
    function swap(uint amount0Out, uint amount1Out, address to, bytes calldata data) external;
    function skim(address to) external;
    function sync() external;

    function initialize(address, address) external;
}
```
<br>

ì´ Interfaceì—ëŠ” UniswapERC20 tokenì˜ interfaceë„ í¬í•¨ë˜ì–´ ìˆëŠ” ê²ƒìœ¼ë¡œ ë³´ì¸ë‹¤. 
ì‹¤ì œ repositoryë¥¼ í™•ì¸í•´ ë³´ë©´ UniswapERC20 tokenì˜ Interfaceë„ ë”°ë¡œ ì¡´ì¬í•œë‹¤.
Pair contractëŠ” Factory contractë¥¼ í†µí•´ pool ë³„ë¡œ ë°°í¬ëœë‹¤. ê·¸ë¦¬ê³  ê·¸ ì•ˆì—ì„œ swapì´ ì¼ì–´ë‚˜ëŠ” ê²ƒìœ¼ë¡œ ë³´ì¸ë‹¤.
ì§ì ‘ ì½”ë“œë¥¼ ë” ë´ì•¼í•  ê²ƒ ê°™ì§€ë§Œ Uniswapì´ë¼ëŠ” ì´ë¦„ì—ì„œ ì•Œ ìˆ˜ ìˆë“¯ì´ `swap` ì´ë¼ëŠ” í•¨ìˆ˜ê°€ ì´ contractì—ì„œ ê°€ì¥ ì¤‘ìš”í•´ ë³´ì¸ë‹¤. `swap`ì„ ì¤‘ì‹¬ìœ¼ë¡œ Pair ì½”ë“œë¥¼ ëœ¯ì–´ë³´ë©° ì•Œì•„ë³´ì!

## Uniswap V2 Factory ì½”ë“œ ë¶„ì„
githubì—ì„œ í™•ì¸í•´ë³´ë©´ ì½”ë“œëŠ” 200 ì¤„ì´ ë„˜ê¸° ë•Œë¬¸ì— ì£¼ì„ì„ í†µí•´ ê°„ë‹¨í•œ í™œìš©ì„±ì„ ë‚¨ê¸°ê³ , ì¤‘ìš”í•œ í•¨ìˆ˜ëŠ” ì½”ë“œ ë’¤ì— ë”°ë¡œ ë‹¤ë£¨ë„ë¡ í•  ê²ƒì´ë‹¤.

### ë³€ìˆ˜
ìš°ì„  ì„ ì–¸ëœ ë³€ìˆ˜ë¶€í„° ì‚´í´ë³´ì. ë³€ìˆ˜ ì„ ì–¸ë¶€í„° ë°°ìš¸ ê²ƒì´ ìƒë‹¹í•˜ë‹¤!
<br>

```solidity
    // uintì— SafeMath ì ìš©
    using SafeMath  for uint;
    // uint224ì— UQ112x112 ì ìš© (UQ112ëŠ” ë¶„ìˆ˜ ê³„ì‚°ì„ í• ë•Œ ì“°ì¸ë‹¤. ë‚˜ì¤‘ì— Libraryë¥¼ í™•ì¸í•´ ë³´ì!)
    using UQ112x112 for uint224;
    
    // liquidityì˜ ìµœì†Ÿê°’ì„ 10**3ìœ¼ë¡œ ì •ì˜
    uint public constant MINIMUM_LIQUIDITY = 10**3;
    // transferì˜ Method IDë¥¼ SELECTOR ë³€ìˆ˜ì— ì„¤ì •
    bytes4 private constant SELECTOR = bytes4(keccak256(bytes('transfer(address,uint256)')));

    // factory contractì˜ address
    address public factory;
    // token0ì˜ address
    address public token0;
    // token1ì˜ address
    address public token1;

    // reserve0, reserve1, blockTimestampLastê°€ í•˜ë‚˜ì˜ storage slotì— ì„ ì–¸ë˜ì–´ìˆë‹¤.
    uint112 private reserve0;           // uses single storage slot, accessible via getReserves
    uint112 private reserve1;           // uses single storage slot, accessible via getReserves
    uint32  private blockTimestampLast; // uses single storage slot, accessible via getReserves

    uint public price0CumulativeLast;
    uint public price1CumulativeLast;
    uint public kLast; // reserve0 * reserve1, as of immediately after the most recent liquidity event

    uint private unlocked = 1;
```
<br>

ë„ˆë¬´ ë§ì€ ë³€ìˆ˜ê°€ ì„ ì–¸ë˜ì–´ ìˆê¸° ë•Œë¬¸ì— ì¤‘ìš”í•˜ê²Œ ìƒê°ë˜ëŠ” ë¶€ë¶„ë§Œ ì„¤ëª…ì„ í•  ê²ƒì´ë‹¤.
1. ìš°ì„  ëˆˆì— ë„ëŠ” ê²ƒì€ `using UQ112x112 for uint224` ì˜€ë‹¤. libraryë¥¼ í™•ì¸í•´ë³´ë‹ˆ overflowë¥¼ ë°©ì§€ ë° ë¶„ìˆ˜ ê³„ì‚°ì„ í•˜ê¸° ìœ„í•´ ë§Œë“¤ì–´ì§„ libraryë¡œ ë³´ì¸ë‹¤.
2. ë‘ë²ˆì§¸ë¡œ `reserve0` `reserve1` `blockTimestampLast`ë¥¼ í•˜ë‚˜ì˜ storage slotì— ë„£ì–´ë‘” ê²ƒì´ë‹¤. 
    - ì—¬ê¸°ì„œ reserve0ì™€ reserve1ì€ uint112, blockTimestampLastëŠ” uint32ë¡œ ì„ ì–¸ë˜ì–´ìˆë‹¤. storageëŠ” í•œ slotë‹¹ 256 bits(32 bytes)ì„ìœ¼ë¡œ ì„¸ ë³€ìˆ˜ë¥¼ ëª¨ë‘ í•˜ë‚˜ì˜ slotì— ë„£ì„ ìˆ˜ ìˆë‹¤. ì´ê²ƒì€ gasë¥¼ saveí•˜ê¸° ìœ„í•œ ê²ƒìœ¼ë¡œ ë³´ì¸ë‹¤. 
    - ë‡Œí”¼ì…œì´ì§€ë§Œ storageë¥¼ ë¶ˆëŸ¬ì˜¬ë•Œ gasê°€ ì‚¬ìš©ë˜ëŠ”ë°, ì—¬ëŸ¬ slotì— ë‚˜ëˆ„ì–´ ì €ì¥í•˜ë©´ slotë§ˆë‹¤ gasë¥¼ ë” ì§€ë¶ˆí•´ì•¼í•œë‹¤. ê·¸ë˜ì„œ í•˜ë‚˜ì˜ slotì— ì €ì¥í•´ í•œë²ˆì— ë¶ˆëŸ¬ì˜¬ ìˆ˜ ìˆê²Œ í•œ ê²ƒë„ gasê°€ê²©ì„ ì¤„ì´ê¸° ìœ„í•œ í•˜ë‚˜ì˜ ë°©ë²•ì¸ ê²ƒ ê°™ë‹¤.
3. MINIMUM_LIQUIDITY: ì ˆëŒ€ 0ìœ¼ë¡œ division ë˜ëŠ” ê²½ìš°ë¥¼ ë§‰ê¸°ìœ„í•´ ìµœì†Œ Liquidityë¥¼ ì§€ì •í–ˆë‹¤

ë‚˜ë¨¸ì§€ ë³€ìˆ˜ëŠ” ì£¼ìš” ì½”ë“œë¥¼ ë¶„ì„í•˜ë©´ì„œ ë³´ë„ë¡í•˜ì! 

<br>

### í•¨ìˆ˜
ì£¼ìš” í•¨ìˆ˜ë¥¼ ì„¤ëª…í•˜ë©´ì„œ ì„¸ë¶€ì ìœ¼ë¡œ callë˜ëŠ” í•¨ê»˜ ì„¤ëª…í•  ì˜ˆì •ì´ë‹¤.
ì£¼ìš” í•¨ìˆ˜ëŠ” `mint`, `burn`, `swap` ê·¸ ì™¸ í•¨ìˆ˜ëŠ” ì´ í•¨ìˆ˜ë“¤ì„ ìœ„í•´ ì¡´ì¬í•œë‹¤ê³  ë³¼ ìˆ˜ ìˆë‹¤.

#### mint

```solidity
function mint(address to) external lock returns (uint liquidity) {
    // getReserves()ë¥¼ í†µí•´ reserve0ê³¼ reserve1ì„ ê°€ì ¸ì˜¨ë‹¤.
    (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
    // contractì— ìˆëŠ” tokenì˜ balanceë¥¼ ë¶ˆëŸ¬ì˜¨ë‹¤.
    uint balance0 = IERC20(token0).balanceOf(address(this));
    uint balance1 = IERC20(token1).balanceOf(address(this));
    // amount ê°’ì„ ê° token balance - reserve ë¡œ ê³„ì‚°í•œë‹¤.
    uint amount0 = balance0.sub(_reserve0);
    uint amount1 = balance1.sub(_reserve1);

    // feeOnì„ í†µí•´ feeì˜ ì—¬ë¶€ë¥¼ ê°€ì ¸ì˜¨ë‹¤.
    bool feeOn = _mintFee(_reserve0, _reserve1);
    uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
    // ì²«ë²ˆì§¸ ë‹¹ì‹œ totalSupplyê°€ 0ì¼ë•Œ ìµœì†Œ totalsupply ê°’ì„ 10**3ìœ¼ë¡œ ì„¤ì •í•œë‹¤. -> ë§¨ ì²˜ìŒ í•œë²ˆë°–ì— ì‹¤í–‰ ì•ˆë¨
    if (_totalSupply == 0) {
        liquidity = Math.sqrt(amount0.mul(amount1)).sub(MINIMUM_LIQUIDITY);
        // lp token 10**3ê°œ ë§Œí¼ mint
       _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
    } else {
        // min ê°’ìœ¼ë¡œ liquidity ì„¤ì •
        liquidity = Math.min(amount0.mul(_totalSupply) / _reserve0, amount1.mul(_totalSupply) / _reserve1);
    }
    // liquidityê°’ì´ 0ë³´ë‹¤ í°ì§€ í™•ì¸
    require(liquidity > 0, 'UniswapV2: INSUFFICIENT_LIQUIDITY_MINTED');
    // liquidity ê°’ ë§Œí¼ address toì— mintë¥¼ ì§„í–‰í•œë‹¤.
    _mint(to, liquidity);

    // reserve ê°’ update
    _update(balance0, balance1, _reserve0, _reserve1);
    if (feeOn) kLast = uint(reserve0).mul(reserve1); // reserve0 and reserve1 are up-to-date
    emit Mint(msg.sender, amount0, amount1);
}
```
<br>

mint í•¨ìˆ˜ëŠ” LP tokenì„ mint í•´ì£¼ëŠ” í•¨ìˆ˜ì´ë‹¤. ì¦‰, periphery contractë¥¼ í†µí•´ ìœ ë™ì„± í’€ì— ìœ ë™ì„±ì„ ê³µê¸‰í• ë•Œ í˜¸ì¶œëœë‹¤. getRerserve()ë¥¼ í†µí•´ ë‘ reserveê°’ì„ ë°›ì•„ì˜¨ë‹¤.

mintì—ëŠ” ë‘ê°€ì§€ ê²½ìš°ê°€ ì¡´ì¬í•œë‹¤. 
1. `totalSupply`ê°€ 0ì„ë•Œ
2. `totalSupply`ê°€ 0ì´ ì•„ë‹ë•Œ

`totalSupply`ê°€ 0ì´ë¼ë©´ liquidityê°’ì„ ì„ì˜ë¡œ êµ¬í•˜ê³ , 1000ê°œì˜ í† í°ì„ zero addressì— mintí•œë‹¤. ìš°ì„  ì„ì˜ë¡œ liquidity ê°’ì„ ì„ì˜ë¡œ êµ¬í•˜ëŠ” ì´ìœ ëŠ” ì²˜ìŒì— ë‘ í† í°ì˜ ìƒëŒ€ì ì¸ ê°€ì¹˜ë¥¼ ì•Œì§€ ëª»í•˜ê¸° ë•Œë¬¸ì´ë¼ê³  docsì— ì¨ìˆì—ˆë‹¤. 

1000ê°œì˜ tokenì„ zero addressì— mintí•˜ëŠ” ì´ìœ ëŠ” zero divisionì„ ë§‰ê¸° ìœ„í•´ì„œì´ë‹¤. zero addressê°€ ì†Œìœ í•˜ê³  ìˆë‹¤ë©´ tokenì„ lockí•  ìˆ˜ ìˆë‹¤. 

`totalSupply`ê°€ 0ì´ ì•„ë‹ˆë¼ë©´, ì¦‰ ìœ„ if ë¬¸ì´ ì´ë¯¸ ì‹¤í–‰ ë˜ì—ˆë‹¤ë©´ liquidityì˜ ê°’ì„ ë‹¤ë¥¸ ë°©ì‹ìœ¼ë¡œ ê³„ì‚°í•œë‹¤. ì´ë•Œ, ìš°ë¦¬ëŠ” í† í°ê°„ì˜ exchange rateì„ ì•Œê³  ìˆê¸° ë•Œë¬¸ì—, ìœ ë™ì„± ì œê³µìê°€ ë™ì¼í•œ ê°€ì¹˜ì˜ í† í°ì„ ì˜ˆì¹˜í•  ìˆ˜ ìˆë„ë¡ í•œë‹¤. 

`liquidity`ì˜ ê³„ì‚° ê°’ì´ 0ë³´ë‹¤ í¬ë‹¤ë©´ `to`ì—ê²Œ LP í† í°ì„ ë°œí–‰í•œë‹¤. ì´í›„ `_update()`ì„ í†µí•´ reserve0ê³¼ reserve1ì˜ ê°’ì„ ì—…ë°ì´íŠ¸í•œë‹¤.
<br>

```solidity
    // update reserves and, on the first call per block, price accumulators
    function _update(uint balance0, uint balance1, uint112 _reserve0, uint112 _reserve1) private {
        require(balance0 <= uint112(-1) && balance1 <= uint112(-1), 'UniswapV2: OVERFLOW');
        uint32 blockTimestamp = uint32(block.timestamp % 2**32);
        uint32 timeElapsed = blockTimestamp - blockTimestampLast; // overflow is desired
        if (timeElapsed > 0 && _reserve0 != 0 && _reserve1 != 0) {
            // * never overflows, and + overflow is desired
            price0CumulativeLast += uint(UQ112x112.encode(_reserve1).uqdiv(_reserve0)) * timeElapsed;
            price1CumulativeLast += uint(UQ112x112.encode(_reserve0).uqdiv(_reserve1)) * timeElapsed;
        }
        reserve0 = uint112(balance0);
        reserve1 = uint112(balance1);
        blockTimestampLast = blockTimestamp;
        emit Sync(reserve0, reserve1);
    }
```
<br>

`_update()`ëŠ” í† í°ì´ deposit ë˜ê±°ë‚˜ withdraw ë  ë•Œ ë¶ˆë¦¬ëŠ” internal í•¨ìˆ˜ì´ë‹¤. requireë¬¸ì„ í†µí•´ balanceë“¤ì˜ overflowë¥¼ ì²´í¬í•œë‹¤. ê·¸ë¦¬ê³  `reserve`ì˜ ê°’ë“¤ê³¼ `blockTimestampLast`ê°’ì„ ìƒˆë¡œìš´ ê°’ìœ¼ë¡œ update í•´ì¤€ë‹¤. 

ì—¬ê¸°ì„œ ìƒê¸°ëŠ” ê¶ê¸ˆì¦ì´ ìˆë‹¤. `price0CumulativeLast` ê°’ì€ ë„ëŒ€ì²´ ì–´ë””ì— ì“°ì´ëŠ”ì§€ ëª¨ë¥´ê² ë‹¤. Defi Mathë¥¼ ê³µë¶€í•˜ë©´ì„œ TWAP(Time weighted average price)ì— ëŒ€í•´ì„œ ë°°ì› ê³ , `price0CumulativeLast`ì™€ `price1CumulativeLast` ê°’ì— ì ìš©ëœ ê²ƒë„ í™•ì¸í•  ìˆ˜ ìˆì—ˆë‹¤. í•˜ì§€ë§Œ ì½”ë“œì—ì„œëŠ” `price0CumulativeLast`ì™€ `price1CumulativeLast`ë¥¼ ì‚¬ìš©í•˜ëŠ” ê³³ì„ ì°¾ì„ ìˆ˜ ì—†ì–´ ì˜ì•„í–ˆë‹¤. (Defi mathë„ ë‹¤ë¥¸ í¬ìŠ¤íŠ¸ì—ì„œ ë‹¤ë£° ì˜ˆì •ì…ë‹ˆë‹¹)

ì•„ë§ˆë„ ë‹¤ë¥¸ ê³³ì—ì„œ uniswap pairì•ˆì˜ í† í° ê°€ê²©ì„ Oracleë¡œ ì œê³µí•˜ê¸° ìœ„í•´ì„œ ì¸ê²ƒ ê°™ë‹¤. ë‚˜ì¤‘ì— ë‹¤ë¥¸ ì½”ë“œë“¤ì„ ë³´ë‹¤ë©´ ì‚¬ìš©ì„±ì„ í™•ì¸ í•  ìˆ˜ ìˆì„ ê²ƒìœ¼ë¡œ ë³´ì¸ë‹¤. ì•„ë§ˆ front-endì—ì„œë„ ì‚¬ìš©ë  ìˆ˜ë„ ìˆì„ ê²ƒ ê°™ë‹¤.
> [UniswapV2 docs](https://docs.uniswap.org/protocol/V2/concepts/core-concepts/oracles)ë¥¼ ì°¾ì•„ë³´ë‹ˆ oracleì„ ìœ„í•´ ë§Œë“¤ì–´ì§„ ê²ƒì´ ë§ë‹¤!ã…ã… Defi oracle ê´€ë ¨í•´ì„œ ë¬¸ì œê°€ ë§ì•˜ì§€ë§Œ UniswapV2ì—ì„œ ì•½ê°„ì´ë‚˜ë§ˆ í•´ê²°í•œ ê²ƒìœ¼ë¡œ ë³´ì¸ë‹¤. 

#### burn

```solidity
    function burn(address to) external lock returns (uint amount0, uint amount1) {
        (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
        address _token0 = token0;                                // gas savings
        address _token1 = token1;                                // gas savings
        uint balance0 = IERC20(_token0).balanceOf(address(this));
        uint balance1 = IERC20(_token1).balanceOf(address(this));
        uint liquidity = balanceOf[address(this)];

        bool feeOn = _mintFee(_reserve0, _reserve1);
        uint _totalSupply = totalSupply; // gas savings, must be defined here since totalSupply can update in _mintFee
        amount0 = liquidity.mul(balance0) / _totalSupply; // using balances ensures pro-rata distribution
        amount1 = liquidity.mul(balance1) / _totalSupply; // using balances ensures pro-rata distribution
        require(amount0 > 0 && amount1 > 0, 'UniswapV2: INSUFFICIENT_LIQUIDITY_BURNED');
        _burn(address(this), liquidity);
        _safeTransfer(_token0, to, amount0);
        _safeTransfer(_token1, to, amount1);
        balance0 = IERC20(_token0).balanceOf(address(this));
        balance1 = IERC20(_token1).balanceOf(address(this));

        _update(balance0, balance1, _reserve0, _reserve1);
        if (feeOn) kLast = uint(reserve0).mul(reserve1); // reserve0 and reserve1 are up-to-date
        emit Burn(msg.sender, amount0, amount1, to);
    }
```

`burn()`ì€ liquidityë¥¼ í•´ì§€(withdraw) í• ë•Œ ì‚¬ìš©ë˜ëŠ” functionì´ë‹¤. burnì€ periphery contractì„ í†µí•´ í˜¸ì¶œëœë‹¤. ì´ë•Œ, pro-rata distributionì„ ì‚¬ìš©í•´ addressì— ëŒë ¤ì¤„ ê°’ì„ êµ¬í•œë‹¤. (liquidity * balance / totalSupplyë¡œ ê³„ì‚°í•œë‹¤.) ë§Œì•½ amountì˜ ê°’ì´ ëª¨ë‘ 0ë³´ë‹¤ í¬ë‹¤ë©´ amountë§Œí¼ì˜ tokenì„ ì´ì²´í•œë‹¤. ì´í›„ liquidityë„ burní•œë‹¤. ê·¸ë¦¬ê³  reserveê°’ì„ updateí•´ì¤€ë‹¤. 

ì´ë•Œ ë³´ë©´ `// gas saving` ì´ë¼ëŠ” ì£¼ì„ì„ ë§ì´ ë³¼ ìˆ˜ ìˆì—ˆë‹¤. ê³„ì‚°ì„ í• ë•Œ storage valì„ ì—¬ëŸ¬ë²ˆ ì‚¬ìš©í•´ì•¼ í•˜ëŠ”ë° ë§Œì•½ ê³„ì† storage val ì‚¬ìš©í•˜ë©´ gas feeê°€ ì˜¬ë¼ê°„ë‹¤. ê·¸ë˜ì„œ storage valì„ local valì— ì €ì¥í•´ local valì„ ê³„ì†í•´ì„œ ì‚¬ìš©í•˜ë„ë¡ í•œê²ƒì´ë‹¤. 

#### swap
```solidity
    // this low-level function should be called from a contract which performs important safety checks
    function swap(uint amount0Out, uint amount1Out, address to, bytes calldata data) external lock {
        require(amount0Out > 0 || amount1Out > 0, 'UniswapV2: INSUFFICIENT_OUTPUT_AMOUNT');
        (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
        require(amount0Out < _reserve0 && amount1Out < _reserve1, 'UniswapV2: INSUFFICIENT_LIQUIDITY');

        uint balance0;
        uint balance1;
        { // scope for _token{0,1}, avoids stack too deep errors
        address _token0 = token0;
        address _token1 = token1;
        require(to != _token0 && to != _token1, 'UniswapV2: INVALID_TO');
        if (amount0Out > 0) _safeTransfer(_token0, to, amount0Out); // optimistically transfer tokens
        if (amount1Out > 0) _safeTransfer(_token1, to, amount1Out); // optimistically transfer tokens
        if (data.length > 0) IUniswapV2Callee(to).uniswapV2Call(msg.sender, amount0Out, amount1Out, data);
        balance0 = IERC20(_token0).balanceOf(address(this));
        balance1 = IERC20(_token1).balanceOf(address(this));
        }
        uint amount0In = balance0 > _reserve0 - amount0Out ? balance0 - (_reserve0 - amount0Out) : 0;
        uint amount1In = balance1 > _reserve1 - amount1Out ? balance1 - (_reserve1 - amount1Out) : 0;
        require(amount0In > 0 || amount1In > 0, 'UniswapV2: INSUFFICIENT_INPUT_AMOUNT');
        { // scope for reserve{0,1} Adjusted, avoids stack too deep errors
        uint balance0Adjusted = balance0.mul(1000).sub(amount0In.mul(3));
        uint balance1Adjusted = balance1.mul(1000).sub(amount1In.mul(3));
        require(balance0Adjusted.mul(balance1Adjusted) >= uint(_reserve0).mul(_reserve1).mul(1000**2), 'UniswapV2: K');
        }

        _update(balance0, balance1, _reserve0, _reserve1);
        emit Swap(msg.sender, amount0In, amount1In, amount0Out, amount1Out, to);
    }
```
<br>

`swap()`ë„ periphery contractë¥¼ í†µí•´ í˜¸ì¶œëœë‹¤.
1. outputì˜ ê°’ì´ 0ë³´ë‹¤ í°ì§€ í™•ì¸í•œë‹¤. 
2. reserveì˜ ê°’ë³´ë‹¤ amountOutì˜ ê°’ì´ ì‘ì€ì§€ í™•ì¸í•œë‹¤. < ë‹¹ì—°íˆ token ë³´ìœ ëŸ‰ë³´ë‹¤ amountOutì˜ ê°’ì´ ì ì–´ì•¼í•œë‹¤.
3. toì˜ ê°’ì´ token0, ë˜ëŠ” token1ì˜ ì£¼ì†Œ ê°’ì´ ì•„ë‹Œ ê²ƒì„ í™•ì¸í•œë‹¤.
4. amountOutì˜ ê°’ë§Œí¼ toì—ê²Œ ì†¡ê¸ˆí•œë‹¤.
5. ì´í›„ ë“¤ì–´ì˜¨ í† í°ì˜ ê°œìˆ˜ë¥¼ ì‚¬ìš©í•´ amountInì˜ ê°’ì„ ê³„ì‚°í•œë‹¤.
6. balanceì˜ ê°’ì„ ë‹¤ì‹œ êµ¬í•œí•œë‹¤.
7. adjustëœ balanceì˜ ê°’ê³¼ reserveì˜ ê°’ì˜ ê°’ê³¼ ë¹„êµí•œë‹¤.
8. reserveì˜ ê°’ì„ updateí•œë‹¤.

ë‚´ê°€ ì—¬ê¸°ì„œ ê¶ê¸ˆí–ˆë˜ ì ì€ swapì€ í•˜ë‚˜ì˜ í† í°ì„ ë‹¤ë¥¸ í† í°ìœ¼ë¡œ êµí™˜í•˜ëŠ” ê²ƒì¸ë°, ë‘ tokenì— `_safeTransfer`ì„ ì‚¬ìš©í•˜ëŠ”ì§€ ì˜€ë‹¤. ê¶ê¸ˆì¦ì„ í•´ì†Œí•˜ê¸° ìœ„í•´ ì ì‹œ periphery contractë¥¼ í™•ì¸í•´ ë³´ì•˜ë‹¤. periphery contractì—ì„œ `swap()`ë¥¼ callí•  ë•ŒëŠ” ë‘˜ì¤‘ì˜ í•˜ë‚˜ì˜ ê°’ì´ ë¬´ì¡°ê»€ 0ì´ ë˜ê²Œ ë§Œë“¤ì–´ ë†“ì•˜ë‹¤. ì´ ë¶€ë¶„ì€ periphery contractì„ ì‚´í´ë³¼ë•Œ ë” ìì„¸íˆ ë“¤ì—¬ë‹¤ë³´ì!

amountInì˜ ê°’ì„ êµ¬í• ë•ŒëŠ” í˜„ì¬ token balanceì™€ ì„¤ì •ëœ reserveê°’ + amountOutê°’ì„ ë¹„êµí•´ amountInì˜ ê°’ì„ êµ¬í•œë‹¤. 

## ë§ˆë¬´ë¦¬
ë‚˜ë¦„ëŒ€ë¡œ ë¶„ì„ì„ ì§„í–‰í•´ë´¤ì§€ë§Œ ì•„ì§ ë¶€ì¡±í•œ ì ì´ ë§ì€ ê²ƒ ê°™ë‹¤ê³  ëŠë‚€ë‹¤. ë” ë§ì€ defi contractë¥¼ ë¶„ì„í•˜ë‹¤ë³´ë©´ ì „ì²´ì ì¸ êµ¬ì¡° ë° ì½”ë“œê°€ ì‘ì„±ëœ ì´ìœ ë¥¼ íŒŒì•…í•˜ëŠ”ë° ë„ì›€ì´ ë  ê²ƒ ê°™ë‹¤. ê·¸ë¦¬ê³  ì´ë²ˆê¸€ì€ êµ¬ì¡°ì ìœ¼ë¡œ ì¡°ê¸ˆ ë¶€ì¡±í•œ ê²ƒ ê°™ì•„ ë” êµ¬ì¡°ê°€ ì¡í˜€ìˆëŠ” ê¸€ì„ ì“°ê¸°ìœ„í•´ ë…¸ë ¥ í•  ê²ƒì´ë‹¤. ë‹¤ìŒìœ¼ë¡œëŠ” periphery contractì™€ UniswapV2 Defi Mathì— ëŒ€í•´ì„œ ë‹¤ë£° ì˜ˆì •ì´ë‹¤. Defië¥¼ ê³µë¶€í•˜ëŠ” ëª¨ë‘ í™”ì´íŒ…! 

## Ref
- https://ethereum.org/en/developers/tutorials/uniswap-v2-annotated-code/#pair-external
- https://docs.uniswap.org/protocol/V2/concepts/core-concepts/swaps
- https://boohyunsik.tistory.com/9?category=922110

```toc

``